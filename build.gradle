plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"

}

def TEAM = 253
def ROBOT_CLASS = "org.usfirst.frc253.Code2018.Robot"

task generateProfiles(type:Exec) {
    workingDir 'profiles'
    commandLine 'java', '-jar', '../generation/path-util-1.2.jar'
}

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << "roborio"
        }
        fileCollectionArtifact('motionProfiles') {
            dependsOn("generateProfiles")
            targets << 'roborio'
            files = fileTree(dir: 'profiles/generated', include: "**/*.csv")
            directory = "/home/lvuser/profiles"
        }
    }
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Phoenix (Talon SRX)
// and NavX.
dependencies {
    //general dependencies
    compile wpilib()
    compile ctre()
    compile navx()
    compile openrio.powerup.matchData()
    compile pathfinder()
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
